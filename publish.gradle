import org.gradle.internal.os.OperatingSystem

import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'maven-publish'

configurations {
    archives
}

publishing {
    repositories {
        maven {
            url "${System.getProperty('user.home')}/releases/maven/development"
        }
        maven {
            url "${System.getProperty('user.home')}/releases/maven/release"
        }
    }
}

ext.version = "2.2"
ext.libVersion = "${ext.version.replace(".", "")}"

def getTriplet() {
    def triplet
    def os_name = System.getProperty("os.name")
    def os_arch = System.getProperty("os.arch")

    if (os_arch == 'amd64') {
        os_arch = 'x64'
    } else if (os_arch == 'i386') {
        os_arch = 'x86'
    } else if (os_arch == 'aarch64' || os_arch == 'arm64') {
        os_arch = 'arm64'
    }

    if (OperatingSystem.current().isWindows()) {
        triplet = "${os_arch}-windows-static-md"
    } else if (OperatingSystem.current().isLinux()) {
        triplet = "${os_arch}-linux"
    } else if (OperatingSystem.current().isMacOsX()) {
        triplet = "${os_arch}-osx"
    } else {
        triplet = "${os_arch}-${os_name}"
    }
    return triplet
}

def getPlatformPath(triplet) {
    if (triplet == "arm32-linux") {
        return "linux/arm32"
    } else if (triplet == "arm64-linux") {
        return "linux/arm64"
    } else if (triplet == "x64-linux") {
        return "linux/x86-64"
    } else if (triplet == "x64-osx") {
        return "osx/x86-64"
    } else if (triplet == "arm64-osx") {
        return "osx/arm64"
    } else if (triplet == "x64-windows-static-md") {
        return "windows/x86-64"
    } else if (triplet == "arm64-windows-static-md") {
        return "windows/arm64"
    } else {
        return ""
    }
}
def triplet
if (!project.hasProperty('platform')) {
    println "No 'platform' property specified; using the build system's platform"
    triplet = getTriplet()
} else {
    triplet = project.platform
}

if (project.hasProperty('forcealternatemacbuild')) {
    if (project.platform == 'arm64-osx') {
        triplet = 'x64-osx'
    } else if (ext.platform == 'x64-osx') {
        triplet = 'arm64-osx'
    }
}

ext.platformPath = getPlatformPath(triplet)
ext.platformClassifier = ext.platformPath.replaceFirst('/', '')

ext.repo = "thirdparty-ceres"
def pubVersion = "$ext.version-3"

def outputsFolder = file("$project.buildDir/outputs")

def baseArtifactId = 'ceres'
def artifactGroupId = 'edu.wpi.first.thirdparty.frc2024.ceres'
def zipBaseName = '_GROUP_edu_wpi_first_thirdparty_frc2024_ceres_ID_ceres-cpp_CLS'

def versionFile = file("$outputsFolder/version.txt")
def licenseFile = file("LICENSE.md")

def outputClassifierStatic = project.ext.platformClassifier + 'static'
println triplet

task copyAllOutputs(type: Copy) {
    destinationDir = outputsFolder
}

ext.addTaskToCopyAllOutputs = { task ->
    copyAllOutputs.dependsOn task
    copyAllOutputs.inputs.file task.archiveFile
    copyAllOutputs.from task.archiveFile
}

task buildVcpkg(type: Exec) {
    def baseArgs = ['install', "--overlay-ports=$rootDir/overlayPorts"]
    outputs.dir "vcpkg_installed/$triplet"
    executable "$rootDir/vcpkg/vcpkg"
    args baseArgs + "--triplet=$triplet"
}

task outputVersions() {
    description = 'Prints the versions of this to a file for use by the downstream packaging project'
    group = 'Build'
    outputs.files(versionFile)

    doFirst {
        buildDir.mkdir()
        outputsFolder.mkdir()
    }

    doLast {
        versionFile.write pubVersion
    }
}

task cppHeadersZip(type: Zip) {
    dependsOn outputVersions
    destinationDirectory = outputsFolder
    archiveBaseName = zipBaseName
    archiveClassifier = "headers"

    from(licenseFile) {
        into '/'
    }
    def resolvedDir = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve(triplet).resolve("include")
    from(resolvedDir.toFile()) {
        into '/'
    }

    includeEmptyDirs = false
    dependsOn buildVcpkg
}

task mergeStaticLibs(type: Exec) {
    def buildDirectory = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve(triplet)

    if (project.platformPath.startsWith('windows')) {
        workingDir buildDirectory.resolve("lib").toString()
        executable 'lib'
        def inputFiles = ["ceres", "gflags_static", "GKlib", "glog", "lapack", "libamd", "libbtf", "libcamd", "libccolamd", "libcholmod", "libcolamd", "libcxsparse", "libklu", "libldl", "libspqr", "libumfpack", "metis", "openblas", "suitesparseconfig", "libf2c"]
        def setArgs = ["/OUT:ceres${project.libVersion}.lib"]
        inputFiles.each {
            setArgs << it + ".lib"
        }
        args = setArgs
    } else if (project.platformPath.startsWith('osx')) {
        executable 'libtool'
        workingDir buildDirectory.resolve("lib").toString()
        
        def outputLibName = "libceres${project.libVersion}.a"

        def inputFiles = ["libspqr", "libldl", "libceres", "libccolamd", "libklu", "libcxsparse", "libglog", "libsuitesparseconfig", "libbtf", "libmetis", "libumfpack", "libcholmod", "libcamd", "libgflags", "libamd", "libGKlib", "libcolamd"]
        
        args '-static', '-o', outputLibName

        inputFiles.each {
            args "${buildDirectory.resolve("lib")}/$it" + ".a"
        }

        inputs.files inputFiles.collect { buildDirectory.resolve("lib").resolve(it) }
        outputs.file buildDirectory.resolve("lib/${outputLibName}")
    } else {
        workingDir buildDirectory.resolve("lib").toString()
        executable 'ar'
        args = ['-M']
        def inputFiles = ["libamd", "libbtf", "libcamd", "libccolamd", "libceres", "libcholmod", "libcolamd","libcxsparse", "libgflags", "libGKlib", "libglog", "libklu", "liblapack", "libldl", "libmetis", "libopenblas", "libspqr", "libsuitesparseconfig", "libumfpack"]

        def inputString = "create libceres${project.libVersion}.a\n"
        inputFiles.each {
            def inFile = 'addlib ' + it + ".a\n"
            inputString += inFile
        }

        inputString += "save\nend\n"
        standardInput = new ByteArrayInputStream(inputString.getBytes())
    }
}

task mergeDebugStaticLibs(type: Exec) {
    def buildDirectory = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve(triplet).resolve("debug")

    if (project.platformPath.startsWith('windows')) {
        workingDir buildDirectory.resolve("lib").toString()
        executable 'lib'
        def inputFiles = ["ceres-debug", "gflags_static_debug", "GKlib", "glog", "lapack", "libamdd", "libbtfd", "libcamdd", "libccolamdd", "libcholmodd", "libcolamdd", "libcxsparsed", "libklud", "libldld", "libspqrd", "libumfpackd", "metis", "openblas", "suitesparseconfigd", "libf2c"]
        def setArgs = ["/OUT:ceres-debug${project.libVersion}.lib"]
        inputFiles.each {
            setArgs << it + ".lib"
        }
        args = setArgs
    } else if (project.platformPath.startsWith('osx')) {
        executable 'libtool'
        workingDir buildDirectory.resolve("lib").toString()
        
        def outputLibName = "libceres-debug${project.libVersion}.a"

        def inputFiles = ["libsuitesparseconfigd", "libspqrd", "libamdd", "libcxsparsed", "libldld", "libglog", "libgflags_debug", "libcolamdd", "libmetis", "libccolamdd", "libcamdd", "libumfpackd", "libklud", "libcholmodd", "libceres-debug", "libbtfd", "libGKlib"]
        
        args '-static', '-o', outputLibName

        inputFiles.each {
            args "${buildDirectory.resolve("lib")}/$it" + ".a"
        }

        inputs.files inputFiles.collect { buildDirectory.resolve("lib").resolve(it) }
        outputs.file buildDirectory.resolve("lib/${outputLibName}")
    } else {
        workingDir buildDirectory.resolve("lib").toString()
        executable 'ar'
        args = ['-M']
        def inputFiles = ["libamdd", "libbtfd", "libcamdd", "libccolamdd", "libceres-debug", "libcholmodd", "libcolamdd", "libcxsparsed", "libgflags_debug", "libGKlib", "libglog", "libklud", "liblapack", "libldld", "libmetis", "libopenblas", "libspqrd", "libsuitesparseconfigd", "libumfpackd"]

        def inputString = "create libceres-debug${project.libVersion}.a\n"
        inputFiles.each {
            def inFile = 'addlib ' + it + ".a\n"
            inputString += inFile
        }

        inputString += "save\nend\n"
        standardInput = new ByteArrayInputStream(inputString.getBytes())
    }
}

task cppLibsZipStatic(type: Zip) {
    dependsOn mergeStaticLibs
    destinationDirectory = outputsFolder
    archiveClassifier = outputClassifierStatic
    archiveBaseName = zipBaseName
    duplicatesStrategy = 'exclude'

    from(licenseFile) {
        into '/'
    }
    def resolvedDir = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve(triplet).resolve("lib")
    from(resolvedDir.toFile()) {
        into project.platformPath + '/static'
        exclude '**/pkgconfig'
        include "**/libceres${project.libVersion}.a"
        include "**/ceres${project.libVersion}.lib"
        include '**/*.pdb'
    }
    dependsOn buildVcpkg
}

task cppLibsZipStaticDebug(type: Zip) {
    dependsOn mergeDebugStaticLibs
    destinationDirectory = outputsFolder
    archiveClassifier = outputClassifierStatic + 'debug'
    archiveBaseName = zipBaseName
    duplicatesStrategy = 'exclude'

    from(licenseFile) {
        into '/'
    }
    def resolvedDir = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve(triplet).resolve("debug").resolve("lib")
    from(resolvedDir.toFile()) {
        into project.platformPath + '/static'
        exclude '**/pkgconfig'
        include "**/libceres-debug${project.libVersion}.a"
        include "**/ceres-debug${project.libVersion}.lib"
        include '**/*.pdb'
    }
    dependsOn buildVcpkg
}

if (!project.hasProperty('skipRelease')) {
    addTaskToCopyAllOutputs(cppLibsZipStatic)
}

if (!project.hasProperty('skipDebug')) {
    addTaskToCopyAllOutputs(cppLibsZipStaticDebug)
}

model {
    publishing {
        publications {
            cpp(MavenPublication) {
                artifact cppHeadersZip
                artifact cppLibsZipStatic
                artifact cppLibsZipStaticDebug

                artifactId = "${baseArtifactId}-cpp"
                groupId artifactGroupId
                version pubVersion
            }
        }
        repositories {
            maven {
                url "${System.getProperty('user.home')}/releases/maven/${project.repo}"
            }
        }
    }
}
